\section{Bitmaps}

TinyVicky allows for three full screen bitmaps to be displayed at once. These bitmaps are either $320 \times 200$ or $320 \times 240$, depending on the value of the CLK\_70 bit of the master control register. A bitmap's pixel data contains either 64,000 bytes, or 76,800 bytes of data. In both cases, the pixel data is arranged from left to right and top to bottom. The first 320 bytes are the pixels of the first line (with the first pixel being the left-most). The second 320 bytes are the second line, and so on. Additionally, the bitmaps can independently use any of the four graphics CLUTs to specify the colors for those indexes. TinyVicky provides registers for each bitmap set the CLUT and the address of the bitmap:

\begin{table}[ht]
    \begin{center}
        \begin{tabular}{|c|c|c|c|c|c|c|c|c|c|c|} \hline
            Address & R/W & Bitmap & 7 & 6 & 5 & 4 & 3 & 2 & 1 & 0 \\ \hline\hline
            \verb+0xD100+ & R/W & \multirow{4}{*}{0} & \multicolumn{5}{|c|}{---} & \multicolumn{2}{|c|}{CLUT} & ENABLE \\\cline{1-2}\cline{4-11}
            \verb+0xD101+ & R/W & & AD7 & AD6 & AD5 & AD4 & AD3 & AD2 & AD1 & AD0 \\\cline{1-2}\cline{4-11}
            \verb+0xD102+ & R/W & & AD15 & AD14 & AD13 & AD12 & AD11 & AD10 & AD9 & AD8 \\\cline{1-2}\cline{4-11}
            \verb+0xD103+ & R/W & & \multicolumn{6}{|c|}{---} & AD17 & AD16 \\ \hline

            \verb+0xD108+ & R/W & \multirow{4}{*}{1} & \multicolumn{5}{|c|}{---} & \multicolumn{2}{|c|}{CLUT} & ENABLE \\\cline{1-2}\cline{4-11}
            \verb+0xD109+ & R/W & & AD7 & AD6 & AD5 & AD4 & AD3 & AD2 & AD1 & AD0 \\\cline{1-2}\cline{4-11}
            \verb+0xD10A+ & R/W & & AD15 & AD14 & AD13 & AD12 & AD11 & AD10 & AD9 & AD8 \\\cline{1-2}\cline{4-11}
            \verb+0xD10B+ & R/W & & \multicolumn{6}{|c|}{---} & AD17 & AD16 \\ \hline

            \verb+0xD110+ & R/W & \multirow{4}{*}{2} & \multicolumn{5}{|c|}{---} & \multicolumn{2}{|c|}{CLUT} & ENABLE \\\cline{1-2}\cline{4-11}
            \verb+0xD111+ & R/W & & AD7 & AD6 & AD5 & AD4 & AD3 & AD2 & AD1 & AD0 \\\cline{1-2}\cline{4-11}
            \verb+0xD112+ & R/W & & AD15 & AD14 & AD13 & AD12 & AD11 & AD10 & AD9 & AD8 \\\cline{1-2}\cline{4-11}
            \verb+0xD113+ & R/W & & \multicolumn{6}{|c|}{---} & AD17 & AD16 \\ \hline
        \end{tabular}
    \end{center}
    \caption{Bitmap Registers}
    \label{tab:bm_registers}
\end{table}

\begin{description}
    \item[ENABLE] if set and both graphics and bitmaps are enabled in the Vicky Master Control Register (see table~\ref{tab:vky_master_ctrl_reg}), then this bitmap will be displayed.

    \item[CLUT] sets the graphics color lookup table to be used for this bitmap

    \item[AD] give the address of the first byte of the pixel data within the 256 KB system RAM. Note that this address is relative to the system bus of 20 bits and is not based on the CPU's addressing.
\end{description}

To set up and display a bitmap, the following things need to be done. The order is not terribly important, although updates to the bitmap's pixel data after the bitmap is displaying will be visible. That could be desirable, depending on what the program is doing.

\begin{enumerate}
    \item Enable bitmap graphics in the TinyVicky Master Control Register (see table:~\ref{tab:vky_master_ctrl_reg}). This means you need to set both the GRAPH and BITMAP bits and either clear TEXT or set the OVRLY to display text and bitmap together.

    \item Set up the pixel data for the bitmap somewhere in the first 256 KB of RAM.

    \item Set the address of the bitmap's pixel data in the AD field.

    \item Assign the bitmap to a layer using the layer control registers (see table:~\ref{tab:bm_tm_layers}).

    \item Set the bitmap's CLUT and ENABLE bit in its control register.
\end{enumerate}

\example{Display a Bitmap}

This example will build on the previous examples of setting up the CLUT and display a gradient on the screen. First, it needs to turn on the bitmap graphics:

\begin{verbatim}
            MMU_MEM_CTRL = $0000            ; MMU Memory Control Register
            MMU_IO_CTRL = $0001             ; MMU I/O Control Register
            VKY_MSTR_CTRL_0 = $D000         ; Vicky Master Control Register 0
            VKY_MSTR_CTRL_1 = $D001         ; Vicky Master Control Register 1
            VKY_BM0_CTRL = $D100            ; Bitmap #0 Control Register
            VKY_BM0_ADDR_L = $D101          ; Bitmap #0 Address bits 7..0
            VKY_BM0_ADDR_M = $D102          ; Bitmap #0 Address bits 15..8
            VKY_BM0_ADDR_H = $D103          ; Bitmap #0 Address bits 17..16

            bitmap_base = $10000            ; The base address of our bitmap

            stz MMU_IO_CTRL     ; Go back to I/O page #0

            lda #$0C            ; enable GRAPHICS and BITMAP. Disable TEXT
            sta VKY_MSTR_CTRL_0 ; Save that to VICKY master control register 0
            stz VKY_MSTR_CTRL_1 ; Make sure we're just in 320x240 mode (VICKY master control register 1)
\end{verbatim}

Next, it needs to set up the bitmap: setting the address, CLUT, and enabling the bitmap:

\begin{verbatim}
            ;
            ; Turn on bitmap #0
            ;

            stz VKY_BM1_CTRL    ; Make sure bitmap 1 is turned off

            lda #$01            ; Use graphics LUT #0, and enable bitmap
            sta VKY_BM0_CTRL

            lda #<bitmap_base   ; Set the low byte of the bitmap's address
            sta VKY_BM0_ADDR_L
            lda #>bitmap_base   ; Set the middle byte of the bitmap's address
            sta VKY_BM0_ADDR_M
            lda #`bitmap_base   ; Set the upper two bits of the bitmap's address
            and #$03
            sta VKY_BM0_ADDR_H
\end{verbatim}

Now, the code needs to create the pixel data for the gradient in memory. This is a bit tricky on the \jr, because the program is using the larger $320 \times 240$ screen, which requires more than 64 KB of memory. In order to write to the entire bitmap, the program will have to work with the MMU to switch memory banks to access the whole bitmap. The program will use bank 1 (0x2000 -- 0x3FFF) as its window into the bitmap, which will start at 0x10000. It will walk through the memory byte-by-byte, setting each pixel's color based on what line it is on (tracked in a \verb+line+ variable). Once it has written a bank's worth of pixels (8 KB), it will increment the bank number and update the MMU register. Once it has written 240 lines, it will finish.

NOTE: in the following code, \verb+bm_bank+ and \verb+line+ are byte variables, and \verb+pointer+ and \verb+column+ are two-byte variables in zero page (although really only \verb+pointer+ has to be there).

\begin{verbatim}
            ; Set the line number to 0
            stz line

            ; Calculate the bank number for the bitmap
            lda #(bitmap_base >> 13)
            sta bm_bank

bank_loop:  stz pointer         ; Set the pointer to start of the current bank
            lda #$20
            sta pointer+1

            ; Set the column to 0
            stz column
            stz column+1

            ; Alter the LUT entries for $2000 -> $bfff

            lda #$80            ; Turn on editing of MMU LUT #0, and work off #0
            sta MMU_MEM_CTRL

            lda bm_bank
            sta MMU_MEM_BANK_1  ; Set the bank we will map to $2000 - $3fff

            stz MMU_MEM_CTRL    ; Turn off editing of MMU LUT #0

            ; Fill the line with the color..

loop2:      lda line            ; The line number is the color of the line
            sta (pointer)

inc_column: inc column          ; Increment the column number
            bne chk_col
            inc column+1

chk_col:    lda column          ; Check to see if we have finished the row
            cmp #<320
            bne inc_point
            lda column+1
            cmp #>320
            bne inc_point

            lda line            ; If so, increment the line number
            inc a
            sta line
            cmp #240            ; If line = 240, we're done
            beq done

            stz column          ; Set the column to 0
            stz column+1

inc_point:  inc pointer         ; Increment pointer
            bne loop2           ; If < $4000, keep looping
            inc pointer+1
            lda pointer+1
            cmp #$40
            bne loop2

            inc bm_bank         ; Move to the next bank
            bra bank_loop       ; And start filling it

done:       nop                 ; Lock up here
            bra done
\end{verbatim}
